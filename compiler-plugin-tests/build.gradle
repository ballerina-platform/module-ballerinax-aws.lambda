/*
 * Copyright (c) 2020   , WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply from: "$rootDir/gradle/javaProject.gradle"

dependencies {
    
    implementation project(':awslambda-compiler-plugin')

    testImplementation "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"
    testImplementation "org.ballerinalang:ballerina-parser:${ballerinaLangVersion}"
    testImplementation "org.ballerinalang:ballerina-tools-api:${ballerinaLangVersion}"
    testImplementation "org.testng:testng:${testngVersion}"
    testImplementation "commons-io:commons-io:${commomsIOVersion}"
    testImplementation "commons-logging:commons-logging:${commonsLoggingVersion}"
    testImplementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    testImplementation "com.google.code.gson:gson:2.7"
    testImplementation "javax.xml.bind:jaxb-api:2.3.1"
    testImplementation "org.glassfish.jaxb:jaxb-runtime:2.3.1"
}

description = 'Ballerinax - AWS Lambda Extension Tests'


spotbugsMain {
    enabled false
}

checkstyleMain {
    enabled false
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

test {
    systemProperties =  [ jacocoAgentLine: "-javaagent:${configurations.jacocoRuntime.asPath}=destfile=${test.jacoco.destinationFile.absolutePath},append=true,inclnolocationclasses=true,exclclassloader=jdk.internal.reflect.DelegatingClassLoader" ,
                          "ballerina.home": System.getProperty('sampleDir', project.projectDir.absolutePath +
                                  "/../target/ballerina-runtime"),
                          sampleDir: System.getProperty('sampleDir', project.projectDir.absolutePath + "/../samples"),
                          ballerinaLayer: System.getProperty('ballerinaPack', project.projectDir.absolutePath + "/../ballerina-layer/bootstrap"),
                          "ballerina.offline.flag": "true"
    ]
    useTestNG() {
        suites "src/test/resources/testng.xml"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    def classFiles = new File("${rootDir}/compiler-plugin/build/classes/java/main/org/ballerinax/awslambda")
    additionalClassDirs(classFiles)
    reports {
        xml.required = true
        html.required = true
        csv.required = true
        xml.destination = new File("${buildDir}/reports/jacoco/report.xml")
        html.destination = new File("${buildDir}/reports/jacoco/report.html")
        csv.destination = new File("${buildDir}/reports/jacoco/report.csv")
    }
}

test.dependsOn ":awslambda-ballerina:build"


// Disable publishing artifacts
tasks.withType(PublishToMavenRepository) {
    enabled = false
}
tasks.withType(PublishToMavenLocal) {
    enabled = false
}
